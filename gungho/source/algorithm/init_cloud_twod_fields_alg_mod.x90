!-------------------------------------------------------------------------------
!(c) Crown copyright 2018 Met Office. All rights reserved.
!The file LICENCE, distributed with this code, contains details of the terms
!under which the code may be used.
!-------------------------------------------------------------------------------

!>@brief Prepares the cloud and twod fields and critical relative humidity, needed
!>       when not initialising from a restart file.

module init_cloud_twod_fields_alg_mod

  use log_mod,                         only: log_event,         &
                                             LOG_LEVEL_INFO

  use constants_mod,                   only: r_def

  use io_mod,                          only: ts_fname
  use io_config_mod,                   only: checkpoint_read, &
                                             restart_stem_name

  use time_config_mod,                 only: timestep_start

  ! Derived Types
  use field_mod,                       only: field_type
  use field_collection_mod,            only: field_collection_type

  ! Handles
  use fs_continuity_mod,               only: Wtheta

  ! Physics options
  use physics_config_mod,              only: cloud_scheme, &
                                             physics_cloud_scheme_none

  implicit none

  private
  public :: init_cloud_twod_fields_alg

contains

  !> @brief Prepares the cloud and twod fields and critical relative humidity and initialises 
  !>        to starting values or from a restart file
  !> @details An algorithm for initialising cloud fields (liquid, ice and bulk cloud 
  !>          fraction), critical relative humidity and twod fields for all solvers.
  !>          The cloud and twod fields need to be initialised so that they have a value
  !>          when passed on to the physics at the first time step, in case no 
  !>          restart file is used. Also, rhcrit is set to the namelist value here.
  !>          In case a restart/checkpoint file is used, clouds, twod fields 
  !>          and rhcrit will be initialised to the values in this restart/checkpoint file
  !> @param[in,out] cloud_fields Cloud related fields 
  !> @param[in,out] twod_fields  Two dimensional fields
  subroutine init_cloud_twod_fields_alg(cloud_fields, twod_fields)

    use initial_cloud_kernel_mod,        only: initial_cloud_kernel_type

    implicit none

    ! Moist fields
    type( field_collection_type ), intent( inout ) :: cloud_fields,twod_fields
    ! Prognostic fields

    type( field_type ), pointer :: cf_area  => null()
    type( field_type ), pointer :: cf_ice  => null()
    type( field_type ), pointer :: cf_liq  => null()
    type( field_type ), pointer :: cf_bulk  => null()
    type( field_type ), pointer :: rhcrit_in_wth  => null()

    type( field_type ), pointer :: tstar_twod  => null()
    type( field_type ), pointer :: zh_twod  => null()
    type( field_type ), pointer :: z0msea_twod  => null()
    type( field_type ), pointer :: ntml_twod  => null()
    type( field_type ), pointer :: cumulus_twod  => null()


    !=== Initialise cloud fields ==================================!

    cf_area => cloud_fields%get_field('area_fraction')
    cf_ice => cloud_fields%get_field('ice_fraction')
    cf_liq => cloud_fields%get_field('liquid_fraction')
    cf_bulk => cloud_fields%get_field('bulk_fraction')
    rhcrit_in_wth => cloud_fields%get_field('rhcrit')

    !=== Initialise twod fields ==================================!

    tstar_twod => twod_fields%get_field('tstar')
    zh_twod => twod_fields%get_field('zh')
    z0msea_twod => twod_fields%get_field('z0msea')
    ntml_twod => twod_fields%get_field('ntml')
    cumulus_twod => twod_fields%get_field('cumulus')

    if (.not. checkpoint_read .or. cloud_scheme == physics_cloud_scheme_none) then  
      ! No check point to start from

      call log_event( "Gungho: Initialising cloud fields", LOG_LEVEL_INFO )

      call invoke(initial_cloud_kernel_type( cf_area, cf_ice, cf_liq, &
                                             cf_bulk, rhcrit_in_wth))

      call log_event( "Gungho: Initialised cloud fields", LOG_LEVEL_INFO )

    else                                   ! Recorded check point to start from

      call log_event("Reading checkpoint file to restart area_fraction",LOG_LEVEL_INFO)
      call cf_area%read_restart("restart_area_fraction", trim(ts_fname(restart_stem_name,"", &
                            "area_fraction", (timestep_start - 1),"")))

      call log_event("Reading checkpoint file to restart ice_fraction",LOG_LEVEL_INFO)
      call cf_ice%read_restart("restart_ice_fraction", trim(ts_fname(restart_stem_name,"", &
                            "ice_fraction", (timestep_start - 1),"")))

      call log_event("Reading checkpoint file to restart liquid_fraction",LOG_LEVEL_INFO)
      call cf_liq%read_restart("restart_liquid_fraction", trim(ts_fname(restart_stem_name,"", &
                            "liquid_fraction", (timestep_start - 1),"")))

      call log_event("Reading checkpoint file to restart bulk_fraction",LOG_LEVEL_INFO)
      call cf_bulk%read_restart("restart_bulk_fraction", trim(ts_fname(restart_stem_name,"", &
                            "bulk_fraction", (timestep_start - 1),"")))

      call log_event("Reading checkpoint file to restart rhcrit",LOG_LEVEL_INFO)
      call rhcrit_in_wth%read_restart("restart_rhcrit", trim(ts_fname(restart_stem_name,"", &
                            "rhcrit", (timestep_start - 1),"")))

      call log_event( "Gungho: Read cloud fields from files", LOG_LEVEL_INFO )
    end if

    if (.not. checkpoint_read) then
      ! No check point to start from

      call log_event( "Gungho: Initialising twod fields", LOG_LEVEL_INFO )

      call invoke( setval_c(tstar_twod, 300.0_r_def))
      call invoke( setval_c(zh_twod, 1000.0_r_def))
      call invoke( setval_c(z0msea_twod, 1.5e-4_r_def))
      call invoke( setval_c(ntml_twod, 1.0_r_def))
      call invoke( setval_c(cumulus_twod, 0.0_r_def))

      call log_event( "Gungho: Initialised twod fields", LOG_LEVEL_INFO )

    else                                   ! Recorded check point to start from

      call log_event("Reading checkpoint file to restart tstar",LOG_LEVEL_INFO)
      call tstar_twod%read_restart("restart_tstar", trim(ts_fname(restart_stem_name,"", &
                            "tstar", (timestep_start - 1),"")))

      call log_event("Reading checkpoint file to restart zh",LOG_LEVEL_INFO)
      call zh_twod%read_restart("restart_zh", trim(ts_fname(restart_stem_name,"", &
                            "zh", (timestep_start - 1),"")))

      call log_event("Reading checkpoint file to restart z0msea",LOG_LEVEL_INFO)
      call z0msea_twod%read_restart("restart_z0msea", trim(ts_fname(restart_stem_name,"", &
                            "z0msea", (timestep_start - 1),"")))

      call log_event("Reading checkpoint file to restart ntml",LOG_LEVEL_INFO)
      call ntml_twod%read_restart("restart_ntml", trim(ts_fname(restart_stem_name,"", &
                            "ntml", (timestep_start - 1),"")))

      call log_event("Reading checkpoint file to restart cumulus",LOG_LEVEL_INFO)
      call cumulus_twod%read_restart("restart_cumulus", trim(ts_fname(restart_stem_name,"", &
                            "cumulus", (timestep_start - 1),"")))

      call log_event( "Gungho: Read twod fields from files", LOG_LEVEL_INFO )
    end if



    !==========================================================================!

  end subroutine init_cloud_twod_fields_alg

end module init_cloud_twod_fields_alg_mod
