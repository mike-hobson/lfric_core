!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!>@brief Initialisation of prognostic fields
module init_prognostic_fields_alg_mod

  use log_mod,                         only: log_event,         &
                                             log_scratch_space, &
                                             LOG_LEVEL_INFO,    &
                                             LOG_LEVEL_TRACE
  use constants_mod,                   only: r_def, i_def

  use solver_mod,                      only: solver_algorithm
  use runtime_constants_mod,           only: get_coordinates, get_height

  ! Configuration and restart/checkpoint options
  use finite_element_config_mod,       only: element_order
  use formulation_config_mod,          only: use_moisture
  use initial_density_config_mod,      only: sample_init_rho, &
                                             set_hydrostatic_density
  use restart_control_mod,             only: restart_type

  ! PsyKAl-lite kernels
  use psykal_lite_mod,                 only: invoke_initial_mr_kernel, &
                                             invoke_initial_rho_sample_kernel

  ! PsyKAl PSYClone kernels
  use set_rho_kernel_mod,              only: set_rho_kernel_type
  use initial_u_kernel_mod,            only: initial_u_kernel_type
  use enforce_bc_kernel_mod,           only: enforce_bc_kernel_type
  use initial_theta_kernel_mod,        only: initial_theta_kernel_type
  use hydrostatic_rho_kernel_mod,      only: hydrostatic_rho_kernel_type

  ! Derived Types
  use field_mod,                       only: field_type
  use quadrature_xyoz_mod,             only: quadrature_xyoz_type
  use quadrature_rule_gaussian_mod,    only: quadrature_rule_gaussian_type

  ! Handles
  use fs_continuity_mod,               only: W3, Wtheta
  use mr_indices_mod,                  only: nummr
  use map_rho_to_theta_mod,            only: map_rho_to_theta
  implicit none

  private
  public :: init_prognostic_fields_alg

contains
  !> @details An algorithm for initialising prognostic fields for 
  !>          all solvers.
  !> @param[in] mesh_id Id of Mesh object on which the model runs
  !> @param[inout] u  The 3D wind field
  !> @param[inout] rho The density
  !> @param[inout] theta The potential temperature
  !> @param[inout] xi The vorticity field 
  !> @param[in] restart Checkpoint/restart type with timestepping information
  subroutine init_prognostic_fields_alg( mesh_id, u, rho, theta, rho_in_wth, mr, xi, restart)

    implicit none

    ! Mesh id
    integer(i_def),  intent(in)           :: mesh_id
    ! Prognostic fields
    type( field_type ), intent( inout )   :: u, rho, theta, xi, mr(nummr), rho_in_wth
    ! Control
    type( restart_type ), intent(in)      :: restart

    type( quadrature_xyoz_type )          :: qr
    type( quadrature_rule_gaussian_type ) :: quadrature_rule
    type( field_type )                    :: r_u
    ! Coordinate fields
    type( field_type ), pointer           :: chi(:) => null(), &
                                             height_wth => null(), &
                                             height_w3 => null()

    integer :: i
    character(5) :: name

    real(kind=r_def) :: initial_time

    qr = quadrature_xyoz_type(element_order+3, quadrature_rule)
    chi => get_coordinates()

    !=== Initialise global prognostic fields ==================================!

    if (.not.restart%read_file()) then           ! No check point to start from

      call log_event( "Gungho: Initialising prognostic fields", LOG_LEVEL_INFO )

      ! Initialise Xi
      call invoke( setval_c( xi, 0.0_r_def ) )
      ! Initialise theta
      call invoke( initial_theta_kernel_type( theta, chi ) )

      initial_time = 0.0_r_def

      ! Initialise U and Rho according to formulation
      r_u = field_type( vector_space = u%get_function_space() )
      call invoke( name = "Initialise U and Rho",   &
                   setval_c( u,   0.0_r_def ),      &
                   setval_c( r_u, 0.0_r_def ),      &
                   initial_u_kernel_type( r_u, chi, initial_time, qr ), &
                   enforce_bc_kernel_type( r_u ) )
      call solver_algorithm(u, r_u)

      if (set_hydrostatic_density) then
        height_w3 => get_height(W3)
        height_wth => get_height(Wtheta)
        call invoke( hydrostatic_rho_kernel_type( rho, theta, height_wth, &
                                                  height_w3 ) )
      else
        if(sample_init_rho) then
          call invoke_initial_rho_sample_kernel( rho, chi, initial_time )
        else
          call invoke( set_rho_kernel_type ( rho, chi, initial_time, qr ) )
        end if
      end if

      call log_event( "Gungho: Mapping rho->rho_in_wth in same space as theta..", LOG_LEVEL_INFO )

      call map_rho_to_theta(rho, theta, rho_in_wth)      
      if (use_moisture) call invoke_initial_mr_kernel( theta, rho_in_wth, mr(:), chi )

      call log_event( "Gungho: Initialised prognostic fields", LOG_LEVEL_INFO )

    else                                   ! Recorded check point to start from

      write(log_scratch_space,'(A,A)') "Reading file:", &
            trim(restart%startfname("rho"))
      call log_event(log_scratch_space,LOG_LEVEL_INFO)

      call rho%read_restart(trim(restart%startfname("rho")))

      write(log_scratch_space,'(A,A)') "Reading file:", &
            trim(restart%startfname("u"))
      call log_event(log_scratch_space,LOG_LEVEL_INFO)

      call u%read_restart(trim(restart%startfname("u")))

      write(log_scratch_space,'(A,A)') "Reading file:", &
            trim(restart%startfname("theta"))
      call log_event(log_scratch_space,LOG_LEVEL_INFO)

      call theta%read_restart(trim(restart%startfname("theta")))

      write(log_scratch_space,'(A,A)') "Reading file:", &
            trim(restart%startfname("xi"))
      call log_event(log_scratch_space,LOG_LEVEL_INFO)

      call xi%read_restart(trim(restart%startfname("xi")))

      if (use_moisture)then
        do i=1,nummr
          write(name, '(A,I2.2)') 'mr_', i
          call mr(i)%read_restart(trim(restart%startfname(name)))
        end do
      end if


      call log_event( "Gungho: Mapping rho->rho_in_wth in same space as theta..", LOG_LEVEL_INFO )
      call map_rho_to_theta(rho, theta, rho_in_wth)

      call log_event( "Gungho: Read prognostic fields from files", LOG_LEVEL_INFO )
    end if

    !==========================================================================!

  end subroutine init_prognostic_fields_alg

end module init_prognostic_fields_alg_mod
