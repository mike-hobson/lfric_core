!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

module compute_tri_precon_kernel_mod_test

  use constants_mod,                       only : i_def, r_def
  use get_unit_test_m3x3_q3x3x3_sizes_mod, only : get_w0_m3x3_q3x3x3_size, &
                                                  get_w3_m3x3_q3x3x3_size
  use get_unit_test_m3x3_dofmap_mod,       only : get_w0_m3x3_dofmap, &
                                                  get_w3_m3x3_dofmap
  use get_unit_test_w3nodal_basis_mod,     only : get_w0_w3nodal_diff_basis
  use timestepping_config_mod,             only : timestepping_runge_kutta_method_ssp4, &
                                                  timestepping_method_semi_implicit
  use pFUnit_Mod
  use yaxt,                                only : xt_initialize, xt_finalize
  use mpi_mod,                             only : store_comm, clear_comm

  implicit none

  private
  public :: compute_tri_precon_test_type, test_all

  @TestCase
  type, extends(MPITestCase) :: compute_tri_precon_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type compute_tri_precon_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: gravity  = 10.0_r_def
  real(r_def), parameter :: radius   = 6000000_r_def
  real(r_def), parameter :: omega    = 8.0E-5_r_def
  real(r_def), parameter :: p_zero   = 100000.0_r_def
  real(r_def), parameter :: rd       = 300.0_r_def
  real(r_def), parameter :: cp       = 1000.0_r_def
  real(r_def), parameter :: scaling  = 1.0_r_def

  integer(i_def), parameter :: method    = timestepping_method_semi_implicit
  integer(i_def), parameter :: rk_method = timestepping_runge_kutta_method_ssp4
  real(r_def),    parameter :: dt = 10.0_r_def
  real(r_def), parameter    :: alpha   = 0.5_r_def
  real(r_def), parameter    :: tau_u   = 0.5_r_def
  real(r_def), parameter    :: tau_t   = 0.5_r_def
  real(r_def), parameter    :: tau_r   = 0.5_r_def
  integer(i_def), parameter :: nout = 1_i_def
  integer(i_def), parameter :: nin  = 1_i_def
  real(r_def), parameter    :: spinup_period = 0.0_r_def
  logical, parameter :: spinup_winds  = .false.
  logical, parameter :: spinup_alpha  = .false.

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config,           &
                                          feign_planet_config,              &
                                          feign_timestepping_config
    implicit none

    class(compute_tri_precon_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    call feign_timestepping_config(    &
         method=method,                &
         dt=dt,                        &
         alpha=alpha,                  &
         tau_u=tau_u,                  &
         tau_t=tau_t,                  &
         tau_r=tau_r,                  &
         outer_iterations=nout,        &
         inner_iterations=nin,         &
         runge_kutta_method=rk_method, &
         spinup_period=spinup_period,  &
         spinup_alpha=spinup_alpha,    &
         spinup_winds=spinup_winds )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(compute_tri_precon_test_type), intent(inout) :: this

    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use compute_tri_precon_kernel_mod, only: compute_tri_precon_code
    use planet_config_mod,             only: kappa

    implicit none

    class(compute_tri_precon_test_type), intent(inout) :: this

    real(r_def), parameter    :: dx = 6000.0_r_def, &
                                 dy = 1000.0_r_def, &
                                 dz = 2000.0_r_def
    real(r_def), parameter    :: tol = 1.0e-10_r_def
    real(r_def)               :: answer

    integer(i_def) :: nlayers, ncells, ncell_3d, cell, icell
    integer(i_def) :: ndf_w0, undf_w0, ndf_w3, undf_w3
    integer(i_def) :: dim_space, dim_space_diff
    integer(i_def) :: nqp_h, nqp_v

    integer(i_def) :: i, j, k

    integer(i_def), allocatable :: map_w0(:,:)
    integer(i_def), allocatable :: map_w3(:,:)
    real(r_def), allocatable :: diff_basis_w0(:,:,:)

    real(r_def), allocatable :: chi1(:), chi2(:), chi3(:)
    real(r_def), allocatable :: A0(:)
    real(r_def), allocatable :: AP(:)
    real(r_def), allocatable :: AM(:)
    real(r_def), allocatable :: theta(:)
    real(r_def), allocatable :: exner(:)
    real(r_def), allocatable :: rho(:)

    nlayers = 3
    call get_w0_m3x3_q3x3x3_size( ndf_w0, undf_w0, ncells, &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v, &
                                  nlayers )
    call get_w3_m3x3_q3x3x3_size( ndf_w3, undf_w3, ncells, &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v, &
                                  nlayers )
    call get_w0_m3x3_dofmap(map_w0)
    call get_w3_m3x3_dofmap(map_w3)
    call get_w0_w3nodal_diff_basis(diff_basis_w0)

    ! Compute coordinates
    allocate(chi1(undf_w0))
    allocate(chi2(undf_w0))
    allocate(chi3(undf_w0))
    icell = 1
    do j = 1,3
      do i = 1,3
        do k = 0,3
          chi1(map_w0(1,icell)+k) = real(i-1)*dx
          chi2(map_w0(1,icell)+k) = real(j-1)*dy
          chi3(map_w0(1,icell)+k) = real(k)*dz
        end do
        icell = icell + 1
      end do
    end do

    ! Create the data
    allocate(A0(undf_w3))
    allocate(AP(undf_w3))
    allocate(AM(undf_w3))
    allocate(exner(undf_w3))
    allocate(rho(undf_w3))
    allocate(theta(undf_w0))
    exner(:) = 1.5_r_def
    rho(:)   = 1.5_r_def
    theta(:) = 300.0_r_def

    cell = 5
    call compute_tri_precon_code( nlayers,                          &
                                  A0,                               &
                                  AP,                               &
                                  AM,                               &
                                  theta,                            &
                                  exner,                            &
                                  rho,                              &
                                  chi1,                             &
                                  chi2,                             &
                                  chi3,                             &
                                  ndf_w3, undf_w3, map_w3(:,cell),  &
                                  ndf_w0, undf_w0, map_w0(:,cell),  &
                                  ndf_w0, undf_w0, map_w0(:,cell),  &
                                  diff_basis_w0                     &
                                 )

    answer = 0.0_r_def
    k = 0
    @assertEqual(answer, AM(map_w3(1, cell)+k), tol)
    k = nlayers - 1
    @assertEqual(answer, AP(map_w3(1, cell)+k), tol)
    
    answer = - (tau_u*dt/dz)**2*cp*theta(map_w0(1, cell))
    k = 1
    @assertEqual(answer, AP(map_w3(1, cell)+k), tol)
    @assertEqual(answer, AM(map_w3(1, cell)+k), tol)
    
    answer = (1.0_r_def - kappa) / kappa * &
              1.0_r_def/exner(map_w3(1, cell)+k) - 2.0_r_def*answer
    @assertEqual(answer, A0(map_w3(1, cell)+k), tol)

    deallocate(map_w0)
    deallocate(map_w3)
    deallocate(diff_basis_w0)

    deallocate(chi1)
    deallocate(chi2)
    deallocate(chi3)
    deallocate(A0)
    deallocate(AP)
    deallocate(AM)
    deallocate(exner)
    deallocate(rho)
    deallocate(theta)

  end subroutine test_all

end module compute_tri_precon_kernel_mod_test
