!-----------------------------------------------------------------------------
! (c) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Test the horizontal linear semi-Lagrangian kernel

module horizontal_linear_sl_kernel_mod_test

  use constants_mod, only : i_def, r_tran

  implicit none

contains

  !------------------------------------------------------------------

  @Test
  subroutine horizontal_linear_sl_kernel_test( )

    use, intrinsic :: iso_fortran_env,  only: real64
    use pFUnit_Mod
    use horizontal_linear_sl_kernel_mod, only: horizontal_linear_sl_code

    implicit none

    real(kind=r_tran), parameter :: tol = 1.0e-12_r_tran   ! r_tran 64-bit
    real(kind=r_tran)            :: answer, use_tol

    integer(kind=i_def), parameter :: nlayers = 1
    integer(kind=i_def), parameter :: ndf_wf = 1
    integer(kind=i_def), parameter :: ndf_w2 = 4
    integer(kind=i_def), parameter :: stencil_size = 9
    integer(kind=i_def), parameter :: stencil_size_c = stencil_size
    integer(kind=i_def), parameter :: extent_size = (stencil_size-1)/4
    integer(kind=i_def), parameter :: undf_w2  = ndf_w2*nlayers
    integer(kind=i_def), parameter :: undf_wf  = ndf_wf*nlayers
    integer(kind=i_def), parameter :: undf_wfs = ndf_wf*nlayers*stencil_size

    integer(kind=i_def), dimension(ndf_wf) :: map_wf
    integer(kind=i_def), dimension(ndf_w2) :: map_w2
    integer(kind=i_def), dimension(ndf_wf,stencil_size_c) :: stencil_map

    real(kind=r_tran),   dimension(undf_wf)  :: field_x
    real(kind=r_tran),   dimension(undf_wf)  :: field_y
    real(kind=r_tran),   dimension(undf_wfs) :: field
    real(kind=r_tran),   dimension(undf_w2)  :: dep_pts_x
    real(kind=r_tran),   dimension(undf_w2)  :: dep_pts_y

    ! Stencil map has form
    !         | 9 |
    !         | 8 |
    ! | 3 | 2 | 1 | 6 | 7 |
    !         | 4 |
    !         | 5 |

    ! Set up maps
    stencil_map(1,:) = (/ 1, 2, 3, 4, 5, 6, 7, 8, 9 /)
    map_w2(:) = (/ 1, 2, 3, 4 /)
    map_wf(1) = 1

    ! Get correct tolerance
    if ( r_tran == real64 ) then
      use_tol = tol
    else
      use_tol = 1.0e-6_r_tran
    end if

    ! Set up field
    field(:) = (/ 1.0_r_tran, 5.0_r_tran, 0.0_r_tran, &
                  2.0_r_tran, 0.0_r_tran, 11.0_r_tran, &
                  0.0_r_tran, 2.0_r_tran, 3.0_r_tran /)

    ! Initialise output fields to zero before each test
    field_x = 0.0_r_tran
    field_y = 0.0_r_tran

    ! Test with integer departure distance
    dep_pts_x(:) = 1.0_r_tran
    dep_pts_y(:) = 1.0_r_tran

    call horizontal_linear_sl_code( nlayers,        &
                                    field_x,        &
                                    field_y,        &
                                    field,          &
                                    stencil_size_c, &
                                    stencil_map,    &
                                    dep_pts_x,      &
                                    dep_pts_y,      &
                                    extent_size,    &
                                    ndf_wf,         &
                                    undf_wfs,       &
                                    map_wf,         &
                                    ndf_w2,         &
                                    undf_w2,        &
                                    map_w2 )

    ! Test updated fields
    answer = 5.0_r_tran
    @assertEqual(answer, field_x, use_tol)
    answer = 2.0_r_tran
    @assertEqual(answer, field_y, use_tol)

    ! Test with fractional, negative departure distance
    dep_pts_x(:) = -0.5_r_tran
    dep_pts_y(:) = -1.5_r_tran

    call horizontal_linear_sl_code( nlayers,        &
                                    field_x,        &
                                    field_y,        &
                                    field,          &
                                    stencil_size_c, &
                                    stencil_map,    &
                                    dep_pts_x,      &
                                    dep_pts_y,      &
                                    extent_size,    &
                                    ndf_wf,         &
                                    undf_wfs,       &
                                    map_wf,         &
                                    ndf_w2,         &
                                    undf_w2,        &
                                    map_w2 )

    ! Test updated fields
    answer = 6.0_r_tran
    @assertEqual(answer, field_x, use_tol)
    answer = 2.5_r_tran
    @assertEqual(answer, field_y, use_tol)

  end subroutine horizontal_linear_sl_kernel_test

end module horizontal_linear_sl_kernel_mod_test
