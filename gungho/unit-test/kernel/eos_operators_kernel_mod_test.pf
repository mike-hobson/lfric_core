!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the equation of state operator computation
!>
module eos_operators_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use pFUnit_Mod
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm
  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: eos_operators_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type eos_operators_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : cellshape_quadrilateral, &
                                          coord_system_xyz
    use feign_config_mod,          only : feign_finite_element_config

    implicit none

    class(eos_operators_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_finite_element_config(           &
             cellshape=cellshape_quadrilateral, &
             coord_order=0_i_def,               &
             coord_system=coord_system_xyz,     &
             element_order=0_i_def,             &
             rehabilitate=.true.,               &
             vorticity_in_w1=.false. )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod,        only: final_configuration

    implicit none

    class(eos_operators_test_type), intent(inout) :: this

    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use eos_operators_kernel_mod,   only : eos_operators_code

    use get_unit_test_m3x3_q3x3x3_sizes_mod, only: get_w0_m3x3_q3x3x3_size, &
                                                   get_w3_m3x3_q3x3x3_size, &
                                                   get_wtheta_m3x3_q3x3x3_size

    use get_unit_test_m3x3_dofmap_mod, only: get_w0_m3x3_dofmap, &
                                             get_w3_m3x3_dofmap, &
                                             get_wtheta_m3x3_dofmap

    use get_unit_test_q3x3x3_basis_mod, only : get_w0_q3x3x3_basis, &
                                               get_w0_q3x3x3_diff_basis, &
                                               get_w3_q3x3x3_basis, &
                                               get_wtheta_q3x3x3_basis

    use get_unit_test_3x3x3_chi_mod, only: get_w0_3x3x3_field

    use get_unit_test_q3x3x3_quadrature_mod, only : get_gaussian_q3x3x3_quadrature_weights_xy, &
                                                    get_gaussian_q3x3x3_quadrature_weights_z
    implicit none

    class(eos_operators_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def

    integer(i_def) :: ncells, cell

    integer(i_def) :: nlayers, nqp_h, nqp_v
    integer(i_def) :: ndf_w0, ndf_w3, ndf_wt
    integer(i_def) :: undf_w0, undf_w3, undf_wt

  ! Dummy variable for passing into multi-getter routines where item is not needed
    integer(i_def) :: unused

    integer(i_def), allocatable :: map_w0(:,:)
    integer(i_def), allocatable :: map_w3(:,:)
    integer(i_def), allocatable :: map_wt(:,:)

    real(r_def), allocatable :: basis_w0(:,:,:,:)
    real(r_def), allocatable :: diff_basis_w0(:,:,:,:)
    real(r_def), allocatable :: basis_w3(:,:,:,:)
    real(r_def), allocatable :: basis_wt(:,:,:,:)

    real(r_def), allocatable :: wh(:), wv(:)

   ! Test field data
    real(r_def), allocatable :: m3exner(:,:,:)
    real(r_def), allocatable :: m3rho(:,:,:)
    real(r_def), allocatable :: p3theta(:,:,:)
    real(r_def), allocatable :: chi_data(:,:)
    real(r_def), allocatable :: exner_data(:)
    real(r_def), allocatable :: rho_data(:)
    real(r_def), allocatable :: theta_data(:)
    real(r_def), allocatable :: panel_id_data(:)

    real(r_def) :: answer

    ! Get canned data for a 3-layer mesh
    nlayers = 3

    ! Get sizes
    call get_w0_m3x3_q3x3x3_size( ndf_w0, undf_w0, ncells, &
                                  unused, unused,          &
                                  nqp_h, nqp_v,            &
                                  nlayers )

    call get_w3_m3x3_q3x3x3_size( ndf_w3, undf_w3, unused, &
                                  unused, unused,          &
                                  unused, unused,          &
                                  nlayers )
    call get_wtheta_m3x3_q3x3x3_size( ndf_wt, undf_wt, unused, &
                                      unused, unused,          &
                                      unused, unused,          &
                                      nlayers )

    ! Get dofmaps
    call get_w0_m3x3_dofmap(map_w0)
    call get_w3_m3x3_dofmap(map_w3)
    call get_wtheta_m3x3_dofmap(map_wt)

    call get_w0_q3x3x3_basis(basis_w0)
    call get_w0_q3x3x3_diff_basis(diff_basis_w0)
    call get_w3_q3x3x3_basis(basis_w3)
    call get_wtheta_q3x3x3_basis(basis_wt)
    call get_gaussian_q3x3x3_quadrature_weights_xy(wh)
    call get_gaussian_q3x3x3_quadrature_weights_z(wv)

    ! Create the data arrays
    allocate(chi_data(undf_w0,3))
    allocate(m3exner(ndf_w3,ndf_w3,ncells))
    allocate(m3rho(ndf_w3,ndf_w3,ncells))
    allocate(p3theta(ndf_w3,ndf_wt,ncells))
    allocate(exner_data(undf_w3))
    allocate(rho_data(undf_w0))
    allocate(theta_data(undf_w0))
    allocate(panel_id_data(undf_w3))

    call get_w0_3x3x3_field(chi_data(:,1), chi_data(:,2), chi_data(:,3), &
                            dx, dy, dz, map_w0, nlayers)

    cell = 1

    ! Create the data
    m3exner(:,:,:)   = 0.0_r_def
    m3rho(:,:,:)     = 0.0_r_def
    p3theta(:,:,:)   = 0.0_r_def
    rho_data(:)      = 1.0_r_def
    exner_data(:)    = 1.5_r_def
    theta_data(:)    = 300.0_r_def
    panel_id_data(:) = 1.0_r_def

    call eos_operators_code( cell,                                    &
                             nlayers,                                 &
                             ncells,                                  &
                             m3exner,                                 &
                             ncells,                                  &
                             m3rho,                                   &
                             ncells,                                  &
                             p3theta,                                 &
                             exner_data,                              &
                             rho_data,                                &
                             theta_data,                              &
                             chi_data(:,1),                           &
                             chi_data(:,2),                           &
                             chi_data(:,3),                           &
                             panel_id_data,                           &
                             0.5_r_def,                               &
                             ndf_w3, undf_w3,                         &
                             map_w3(:,cell), basis_w3,                &
                             ndf_wt,  undf_wt,                        &
                             map_wt(:,cell), basis_wt,                &
                             ndf_w0, undf_w0,                         &
                             map_w0(:,cell), basis_w0, diff_basis_w0, &
                             ndf_w3, undf_w3,                         &
                             map_w3(:,cell),                          &
                             nqp_h, nqp_v, wh, wv                     &
                           )

    answer = 0.5_r_def/exner_data(1)
    @assertEqual(answer, m3exner(1, 1, 1 )/(dx*dy*dz), tol)
    answer = 1.0_r_def/rho_data(1)
    @assertEqual(answer, m3rho(1, 1, 1 )/(dx*dy*dz), tol)
    answer = 0.5_r_def/theta_data(1)
    @assertEqual(answer, p3theta(1, 1, 1 )/(dx*dy*dz), tol)
    @assertEqual(answer, p3theta(1, 2, 1 )/(dx*dy*dz), tol)

    deallocate(map_w0)
    deallocate(map_w3)
    deallocate(map_wt)
    deallocate(basis_w0)
    deallocate(diff_basis_w0)
    deallocate(basis_w3)
    deallocate(basis_wt)
    deallocate(wh)
    deallocate(wv)
    deallocate(chi_data)
    deallocate(m3exner)
    deallocate(m3rho)
    deallocate(p3theta)
    deallocate(exner_data)
    deallocate(rho_data)
    deallocate(theta_data)
    deallocate(panel_id_data)

  end subroutine test_all

end module eos_operators_kernel_mod_test
