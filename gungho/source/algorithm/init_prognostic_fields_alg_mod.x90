!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!>@brief Initialisation of prognostic fields
module init_prognostic_fields_alg_mod

  use log_mod,                         only: log_event,         &
                                             log_scratch_space, &
                                             LOG_LEVEL_INFO,    &
                                             LOG_LEVEL_TRACE
  use constants_mod,                   only: r_def, i_def,      &
                                             str_short
  use mass_matrix_solver_alg_mod,      only: mass_matrix_solver_alg
  use runtime_constants_mod,           only: get_coordinates, &
                                             get_mass_matrix, &
                                             w3inv_id,        &
                                             get_height

  ! Configuration and restart/checkpoint options
  use finite_element_config_mod,       only: element_order
  use formulation_config_mod,          only: use_moisture, set_hydrostatic_init
  use initial_density_config_mod,      only: sample_init_rho
  use initial_wind_config_mod,         only: smp_init_wind
  use io_mod,                          only: ts_fname
  use io_config_mod,                   only: checkpoint_read, &
                                             restart_stem_name
  use time_config_mod,                 only: timestep_start

  ! PsyKAl-lite kernels
  use psykal_lite_mod,                 only: invoke_initial_rho_sample_kernel,  &
                                             invoke_initial_exner_sample_kernel,&
                                             invoke_hydrostatic_exner_kernel
  use field_bundle_mod,                only: set_bundle_scalar

  ! PsyKAl PSYClone kernels
  use set_rho_kernel_mod,              only: set_rho_kernel_type
  use set_exner_kernel_mod,            only: set_exner_kernel_type
  use initial_u_kernel_mod,            only: initial_u_kernel_type
  use enforce_bc_kernel_mod,           only: enforce_bc_kernel_type
  use initial_theta_kernel_mod,        only: initial_theta_kernel_type
  use project_pressure_kernel_mod,     only: project_pressure_kernel_type
  use sample_eos_rho_kernel_mod,       only: sample_eos_rho_kernel_type
  use hydrostatic_exner_kernel_mod,    only: hydrostatic_exner_kernel_type
  use moist_dyn_factors_kernel_mod,    only: moist_dyn_factors_kernel_type
  use initial_mr_kernel_mod,           only: initial_mr_kernel_type

  ! Derived Types
  use field_mod,                       only: field_type
  use quadrature_xyoz_mod,             only: quadrature_xyoz_type
  use quadrature_rule_gaussian_mod,    only: quadrature_rule_gaussian_type
  use sample_initial_u_kernel_mod,     only: sample_initial_u_kernel_type 
  use operator_mod,                    only: operator_type

  ! Handles
  use fs_continuity_mod,               only: W3, Wtheta
  use mr_indices_mod,                  only: nummr, mr_names
  use moist_dyn_mod,                   only: num_moist_factors, gas_law
  implicit none

  private
  public :: init_prognostic_fields_alg

contains

  !> @details An algorithm for initialising prognostic fields for 
  !>          all solvers.
  !> @param[in,out] u 3D Wind field
  !> @param[in,out] rho Density
  !> @param[in,out] theta Potential temperature
  !> @param[in,out] exner Exner pressure
  !> @param[in,out] mr Field bundle containing the moisture mixring ratios
  !> @param[in,out] moist_dyn Auxilliary fields for moist dynamics
  !> @param[in,out] xi The vorticity field 
  subroutine init_prognostic_fields_alg(u, rho, theta, exner,  mr, moist_dyn, &
                                        xi)

    implicit none

    ! Prognostic fields
    type( field_type ), intent( inout ) :: u, rho, theta, exner, xi, mr(nummr), &
                                           moist_dyn(num_moist_factors)

    type( quadrature_xyoz_type )          :: qr
    type( quadrature_rule_gaussian_type ) :: quadrature_rule
    type( field_type )                    :: r_u
    ! Coordinate fields
    type( field_type ), pointer           :: chi(:) => null(), &
                                             height_wth => null(), &
                                             height_w3 => null()

    ! Operators
    type(operator_type), pointer        :: m3_inv => null()

    integer :: i
    character(str_short) :: name

    real(kind=r_def) :: initial_time

    qr = quadrature_xyoz_type(element_order+3, quadrature_rule)
    chi => get_coordinates()

    ! Default is dry dynamics - set moist dynamics factors to one
    do i = 1, num_moist_factors
      call invoke( setval_c( moist_dyn(i), 1.0_r_def) )
    end do

    !=== Initialise global prognostic fields ==================================!

    if (.not. checkpoint_read ) then           ! No check point to start from

      call log_event( "Gungho: Initialising prognostic fields", LOG_LEVEL_INFO )

      ! Initialise Xi
      call invoke( setval_c( xi, 0.0_r_def ) )
      ! Initialise theta
      call invoke( initial_theta_kernel_type( theta, chi ) )

      initial_time = 0.0_r_def

      if (smp_init_wind) then
        call invoke( name = "Initialise U",    &
                     setval_c( u, 0.0_r_def ), &
                     sample_initial_u_kernel_type( u, chi ) )
      else
        ! Initialise U and Rho according to formulation
        r_u = field_type( vector_space =  u%get_function_space() )
        call invoke( name = "Initialise U and Rho",   &
                     setval_c( u,   0.0_r_def ),      &
                     setval_c( r_u, 0.0_r_def ),      &
                     initial_u_kernel_type( r_u, chi, initial_time, qr ), &
                     enforce_bc_kernel_type( r_u ) )
        call mass_matrix_solver_alg(u, r_u)
      end if

      if (set_hydrostatic_init) then
        height_w3 => get_height(W3)
        height_wth => get_height(Wtheta)

        ! Initialize exner assuming hydrostatic balance
        call invoke_hydrostatic_exner_kernel( exner, theta, moist_dyn, height_wth, &
                                              height_w3, chi )
        ! Diagnose density from equation of state
        call invoke( sample_eos_rho_kernel_type( rho, exner, theta,                &
                                                 moist_dyn(gas_law) ) )
      else
        if(sample_init_rho) then
          call invoke_initial_rho_sample_kernel  ( rho,   chi, initial_time )
          call invoke_initial_exner_sample_kernel( exner, chi, initial_time )
        else
          call invoke( set_rho_kernel_type   ( rho,   chi, initial_time, qr ), &
                       set_exner_kernel_type ( exner, chi, initial_time, qr ) )
          call log_event( "Gungho: Computing initial pressure as projection..", LOG_LEVEL_INFO )
          m3_inv => get_mass_matrix(w3inv_id)
          qr = quadrature_xyoz_type(1, quadrature_rule)

          ! Pressure has already been initialized
          call invoke( project_pressure_kernel_type( exner, rho, theta, moist_dyn(gas_law), &
                                                     chi, m3_inv, qr ) )

        end if
      end if

      if (use_moisture) then
        ! Initialise moisture and update factors for moist dynamics
        call invoke( initial_mr_kernel_type( theta, exner, rho, mr, chi ),         &
                     moist_dyn_factors_kernel_type( moist_dyn, mr ) )
        ! Recalculate hydrostatic pressure and dry density
        height_w3 => get_height(W3)
        height_wth => get_height(Wtheta)
        call invoke_hydrostatic_exner_kernel( exner, theta, moist_dyn, height_wth, &
                                              height_w3, chi )
        call invoke( sample_eos_rho_kernel_type( rho, exner, theta,                &
                                                 moist_dyn(gas_law) ) )
      else
        call set_bundle_scalar(0.0_r_def, mr, nummr)
      end if

      call log_event( "Gungho: Initialised prognostic fields", LOG_LEVEL_INFO )

    else                                   ! Recorded check point to start from

      call log_event("Reading checkpoint file to restart rho",LOG_LEVEL_INFO)
      call rho%read_restart("restart_rho", trim(ts_fname(restart_stem_name,"", &
                            "rho", (timestep_start - 1),"")))

      call log_event("Reading checkpoint file to restart u",LOG_LEVEL_INFO)
      call u%read_restart("restart_u", trim(ts_fname(restart_stem_name,"", &
                            "u", (timestep_start - 1),"")))

      call log_event("Reading checkpoint file to restart theta",LOG_LEVEL_INFO)
      call theta%read_restart("restart_theta", trim(ts_fname(restart_stem_name,"", &
                            "theta", (timestep_start - 1),"")))

      call log_event("Reading checkpoint file to restart exner",LOG_LEVEL_INFO)
      call exner%read_restart("restart_exner", trim(ts_fname(restart_stem_name,"", &
                            "exner", (timestep_start - 1),"")))

      call log_event("Reading checkpoint file to restart xi",LOG_LEVEL_INFO)
      call xi%read_restart("restart_xi", trim(ts_fname(restart_stem_name,"", &
                            "xi", (timestep_start - 1),"")))

      if (use_moisture)then
        do i=1,nummr
          write(name, '(A)') 'restart_'//trim(mr_names(i))
          write(log_scratch_space,'(A,A)') "Reading checkpoint file to restart ", trim(name)
          call log_event(log_scratch_space,LOG_LEVEL_INFO)
          call mr(i)%read_restart(trim(name), trim(ts_fname(restart_stem_name,"", &
                            trim(mr_names(i)), (timestep_start - 1),"")))
        end do
        ! Update factors for moist dynamics
        write(log_scratch_space,'(A)') 'Restart step 1: update moist_dyn'
        call log_event(log_scratch_space,LOG_LEVEL_INFO)
        call invoke(moist_dyn_factors_kernel_type( moist_dyn, mr ))
      end if

      call log_event( "Gungho: Read prognostic fields from files", LOG_LEVEL_INFO )
    end if

    !==========================================================================!
    nullify( m3_inv, chi, height_wth, height_w3 )

  end subroutine init_prognostic_fields_alg

end module init_prognostic_fields_alg_mod
