!-----------------------------------------------------------------------------
! (c) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Test the kernel that computes the quintic vertical SL coefficients

module compute_vertical_quintic_coef_kernel_mod_test

  use constants_mod,                  only: i_def, l_def, r_tran

  implicit none

contains

  !------------------------------------------------------------------

  @test
  subroutine compute_vertical_quintic_coef_kernel_test()
    use pFUnit_Mod
    use, intrinsic :: iso_fortran_env,  only: real64
    use compute_vertical_quintic_coef_kernel_mod,        only: compute_vertical_quintic_coef_code

    implicit none

    real(kind=r_tran) :: tol
    integer(kind=i_def), parameter :: nlayers = 6
    integer(kind=i_def), parameter :: ndf_wt = 2
    integer(kind=i_def), parameter :: undf_wt  = (ndf_wt-1)*nlayers+1
    integer(kind=i_def), parameter :: ndf_w2v = 2
    integer(kind=i_def), parameter :: undf_w2v  = (ndf_w2v-1)*nlayers+1
    integer(kind=i_def), parameter :: ndf_wc = 1
    integer(kind=i_def), parameter :: undf_wc  = 6*ndf_wc*nlayers
    integer(kind=i_def), parameter :: ndf_wl = 1
    integer(kind=i_def), parameter :: undf_wl  = 2*ndf_wl*nlayers
    integer(kind=i_def), parameter :: ndf_wm = 2
    integer(kind=i_def), parameter :: undf_wm  = 6*(ndf_wm-1)*nlayers+6
    integer(kind=i_def), parameter :: ndf_wn = 2
    integer(kind=i_def), parameter :: undf_wn  = 2*(ndf_wn-1)*nlayers+2

    integer(kind=i_def), dimension(ndf_wt)  :: map_wt
    integer(kind=i_def), dimension(ndf_w2v) :: map_w2v
    integer(kind=i_def), dimension(ndf_wc)  :: map_wc
    integer(kind=i_def), dimension(ndf_wl)  :: map_wl
    integer(kind=i_def), dimension(ndf_wm)  :: map_wm
    integer(kind=i_def), dimension(ndf_wn)  :: map_wn

    real(kind=r_tran),   dimension(undf_w2v) :: dep_pts_z
    real(kind=r_tran),   dimension(undf_wt)  :: theta_height
    real(kind=r_tran),   dimension(undf_wc)  :: quintic_coef_w3
    real(kind=r_tran),   dimension(undf_wl)  :: linear_coef_w3
    integer(kind=i_def), dimension(undf_wc)  :: quintic_indices_w3
    real(kind=r_tran),   dimension(undf_wm)  :: quintic_coef_wt
    real(kind=r_tran),   dimension(undf_wn)  :: linear_coef_wt
    integer(kind=i_def), dimension(undf_wm)  :: quintic_indices_wt

    ! Set tolerance
    if ( r_tran == real64 ) then
      tol = 1.0e-12_r_tran
    else
      tol = 1.0e-6_r_tran
    end if

    ! Set maps
    map_w2v(1) = 1
    map_w2v(2) = 2
    map_wt(1) = 1
    map_wt(2) = 2
    map_wc(1) = 1
    map_wl(1) = 1
    map_wm(1) = 1
    map_wm(2) = 2
    map_wn(1) = 1
    map_wn(2) = 2

    ! Set up data
    theta_height = (/0.0_r_tran, 1.0_r_tran, 2.0_r_tran, 3.0_r_tran, &
                     4.0_r_tran, 5.0_r_tran, 6.0_r_tran /)
    dep_pts_z    = (/0.0_r_tran, 0.0_r_tran, 0.5_r_tran, 1.2_r_tran, &
                    -0.1_r_tran, 0.0_r_tran, 0.0_r_tran /)

    ! Get W3 coefficients
    call compute_vertical_quintic_coef_code( nlayers,                                             &
                                             dep_pts_z,                                           &
                                             theta_height,                                        &
                                             quintic_coef_w3, quintic_indices_w3, linear_coef_w3, &
                                             ndf_w2v, undf_w2v, map_w2v,                          &
                                             ndf_wt, undf_wt, map_wt,                             &
                                             ndf_wc, undf_wc, map_wc,                             &
                                             ndf_wl, undf_wl, map_wl )

    ! Test selection of the indices (indices are also tested in sl_support_mod_test)
    @assertEqual(1.0_r_tran, real(quintic_indices_w3(1),r_tran), tol)
    @assertEqual(1.0_r_tran, real(quintic_indices_w3(2+nlayers),r_tran), tol)
    @assertEqual(2.0_r_tran, real(quintic_indices_w3(3+2*nlayers),r_tran), tol)
    @assertEqual(4.0_r_tran, real(quintic_indices_w3(4+3*nlayers),r_tran), tol)
    @assertEqual(6.0_r_tran, real(quintic_indices_w3(5+4*nlayers),r_tran), tol)
    @assertEqual(6.0_r_tran, real(quintic_indices_w3(6+5*nlayers),r_tran), tol)

    ! Test the quintic coefficients (coefficients are also tested in sl_support_mod_test)
    @assertEqual(0.0_r_tran, quintic_coef_w3(1), tol)
    @assertEqual(0.0_r_tran, quintic_coef_w3(2+nlayers), tol)
    @assertEqual(1.0_r_tran, quintic_coef_w3(1+2*nlayers), tol)
    @assertEqual(0.25_r_tran, quintic_coef_w3(2+2*nlayers), tol)
    @assertEqual(0.95_r_tran, quintic_coef_w3(5+2*nlayers), tol)
    @assertEqual(0.0_r_tran, quintic_coef_w3(6+5*nlayers), tol)

    ! Get Wtheta coefficients
    call compute_vertical_quintic_coef_code( nlayers,                                             &
                                             dep_pts_z,                                           &
                                             theta_height,                                        &
                                             quintic_coef_wt, quintic_indices_wt, linear_coef_wt, &
                                             ndf_w2v, undf_w2v, map_w2v,                          &
                                             ndf_wt, undf_wt, map_wt,                             &
                                             ndf_wm, undf_wm, map_wm,                             &
                                             ndf_wn, undf_wn, map_wn )

    ! Test selection of the indices (indices are also tested in sl_support_mod_test)
    @assertEqual(1.0_r_tran, real(quintic_indices_wt(1),r_tran), tol)
    @assertEqual(1.0_r_tran, real(quintic_indices_wt(2+(nlayers+1)),r_tran), tol)
    @assertEqual(2.0_r_tran, real(quintic_indices_wt(3+2*(nlayers+1)),r_tran), tol)
    @assertEqual(3.0_r_tran, real(quintic_indices_wt(4+3*(nlayers+1)),r_tran), tol)
    @assertEqual(7.0_r_tran, real(quintic_indices_wt(5+4*(nlayers+1)),r_tran), tol)
    @assertEqual(7.0_r_tran, real(quintic_indices_wt(6+5*(nlayers+1)),r_tran), tol)

    ! Test the quintic coefficients (coefficients are also tested in sl_support_mod_test)
    @assertEqual(0.0_r_tran, quintic_coef_wt(1), tol)
    @assertEqual(-0.032_r_tran, quintic_coef_wt(4+(nlayers+1)), tol)
    @assertEqual(0.5625_r_tran, quintic_coef_wt(3+2*(nlayers+1)), tol)
    @assertEqual(0.216_r_tran, quintic_coef_wt(4+2*(nlayers+1)), tol)
    @assertEqual(1.0_r_tran, quintic_coef_wt(2+3*(nlayers+1)), tol)
    @assertEqual(-0.0625_r_tran, quintic_coef_wt(3+4*(nlayers+1)), tol)

  end subroutine compute_vertical_quintic_coef_kernel_test

end module compute_vertical_quintic_coef_kernel_mod_test