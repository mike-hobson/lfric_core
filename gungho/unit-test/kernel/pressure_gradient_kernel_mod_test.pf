!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the pressure gradient computation
!>
module pressure_gradient_kernel_mod_test

  use constants_mod,                 only : i_def, r_def

  use get_unit_test_m3x3_q3x3x3_sizes_mod, only : get_w2_m3x3_q3x3x3_size,     &
                                                  get_wtheta_m3x3_q3x3x3_size, &
                                                  get_w3_m3x3_q3x3x3_size

  use get_unit_test_m3x3_dofmap_mod,       only : get_w2_m3x3_dofmap,     &
                                                  get_wtheta_m3x3_dofmap, &
                                                  get_w3_m3x3_dofmap

  use get_unit_test_q3x3x3_quadrature_mod, &
     only : get_gaussian_q3x3x3_quadrature_weights_xy, &
            get_gaussian_q3x3x3_quadrature_weights_z

  use get_unit_test_q3x3x3_basis_mod, only: get_w2_q3x3x3_basis,         &
                                            get_w2_q3x3x3_diff_basis,    &
                                            get_w3_q3x3x3_basis,         &
                                            get_wtheta_q3x3x3_basis,     &
                                            get_wtheta_q3x3x3_diff_basis

  use pFUnit_Mod

  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: pressure_gradient_test_type, test_all

  @TestCase
  type, extends(MPITestCase) :: pressure_gradient_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type pressure_gradient_test_type

  real(r_def), parameter :: gravity  = 10.0_r_def
  real(r_def), parameter :: radius   = 6000000_r_def
  real(r_def), parameter :: omega    = 8.0E-5_r_def
  real(r_def), parameter :: p_zero   = 100000.0_r_def
  real(r_def), parameter :: rd       = 300.0_r_def
  real(r_def), parameter :: cp       = 1000.0_r_def
  real(r_def), parameter :: scaling  = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use feign_config_mod,          only : feign_initial_temperature_config, &
                                          feign_planet_config

    implicit none

    class(pressure_gradient_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod,        only: final_configuration

    implicit none

    class(pressure_gradient_test_type), intent(inout) :: this

    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use pressure_gradient_kernel_mod, only : pressure_gradient_code

    implicit none

    class(pressure_gradient_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-3_r_def
    real(r_def), parameter :: answer = 0.0_r_def

    ! Fields
    real(r_def), allocatable :: rho(:)
    real(r_def), allocatable :: theta(:)
    real(r_def), allocatable :: r_u(:)

    integer(i_def) :: i, j, k, cell

    ! Sizes
    integer(i_def)            :: nlayers, ncells, nqp_h, nqp_v
    integer(i_def)            :: ndf_wtheta, ndf_w2, ndf_w3
    integer(i_def)            :: undf_wtheta, undf_w2, undf_w3
    integer(i_def)            :: dim_space, dim_space_diff

    ! Dofmaps
    integer(i_def), allocatable :: map_w2(:,:)
    integer(i_def), allocatable :: map_w3(:,:)
    integer(i_def), allocatable :: map_wtheta(:,:)

   ! Basis functions
    real(r_def),    allocatable :: basis_wtheta(:,:,:,:)
    real(r_def),    allocatable :: diff_basis_wtheta(:,:,:,:)
    real(r_def),    allocatable :: basis_w2(:,:,:,:)
    real(r_def),    allocatable :: diff_basis_w2(:,:,:,:)
    real(r_def),    allocatable :: basis_w3(:,:,:,:)

    ! Weights
    real(r_def),    allocatable :: wh(:)
    real(r_def),    allocatable :: wv(:)

    nlayers = 3

    call get_w2_m3x3_q3x3x3_size( ndf_w2, undf_w2, ncells,     &
                                  dim_space, dim_space_diff,   &
                                  nqp_h, nqp_v,                &
                                  nlayers )
    call get_w3_m3x3_q3x3x3_size( ndf_w3, undf_w3, ncells,   &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v,              &
                                  nlayers )
    call get_wtheta_m3x3_q3x3x3_size( ndf_wtheta, undf_wtheta, ncells, &
                                      dim_space, dim_space_diff,       &
                                      nqp_h, nqp_v,                    &
                                      nlayers )

    call get_w2_m3x3_dofmap(map_w2)
    call get_w3_m3x3_dofmap(map_w3)
    call get_wtheta_m3x3_dofmap(map_wtheta)

    call get_gaussian_q3x3x3_quadrature_weights_xy( wh )
    call get_gaussian_q3x3x3_quadrature_weights_z( wv )

    ! Get canned basis functions
    call get_w2_q3x3x3_basis(basis_w2)
    call get_w2_q3x3x3_diff_basis(diff_basis_w2)
    call get_wtheta_q3x3x3_basis(basis_wtheta)
    call get_wtheta_q3x3x3_diff_basis(diff_basis_wtheta)
    call get_w3_q3x3x3_basis(basis_w3)

    ! Create the data
    allocate( rho( undf_w3 ) )
    allocate( r_u( undf_w2 ) )
    allocate( theta( undf_wtheta ) )

    cell = 1

    ! Create the data
    r_u(:) = 0.0_r_def
    rho(:) = 1.5_r_def
    theta(:) = 300.0_r_def

    call pressure_gradient_code( nlayers,                         &
                                 r_u,                             &
                                 rho,                             &
                                 theta,                           &
                                 ndf_w2, undf_w2,                 &
                                 map_w2(:, cell),                 &
                                 basis_w2, diff_basis_w2,         &
                                 ndf_w3, undf_w3,                 &
                                 map_w3(:, cell),                 &
                                 basis_w3,                        &
                                 ndf_wtheta, undf_wtheta,         &
                                 map_wtheta(:, cell),             &
                                 basis_wtheta, diff_basis_wtheta, &
                                 nqp_h, nqp_v, wh, wv             &
                                 )

    k = 1
    @assertEqual(answer, r_u(map_w2(5, cell)+k), tol)
    @assertEqual(answer, r_u(map_w2(6, cell)+k), tol)

    deallocate(rho)
    deallocate(r_u)
    deallocate(theta)
    deallocate(basis_w2)
    deallocate(diff_basis_w2)
    deallocate(basis_w3)
    deallocate(basis_wtheta)
    deallocate(diff_basis_wtheta)
    deallocate(map_w2)
    deallocate(map_w3)
    deallocate(map_wtheta)
    deallocate(wv)
    deallocate(wh)

  end subroutine test_all

end module pressure_gradient_kernel_mod_test
