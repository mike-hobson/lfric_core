!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Test the functionality of mpi_mod
!>
!> @details A pFUnit test module to exercise the functionality in mpi_mod.
!>
module mpi_mod_test

  use pFUnit_Mod
  use constants_mod, only: i_def, r_def, r_single, r_double, &
                           integer_type, real_type
  use mpi_mod, only: store_comm, clear_comm, &
                     global_sum, global_max, global_min, &
                     all_gather, broadcast, &
                     get_mpi_datatype, &
                     get_comm_size, get_comm_rank
  use yaxt, only : xt_initialize, xt_finalize

  implicit none

  private
  public test_all

  @TestCase
  type, extends(MPITestCase), public :: mpi_mod_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type mpi_mod_test_type

contains
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    implicit none

    class(mpi_mod_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(mpi_mod_test_type), intent(inout) :: this

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all(this)

    use mpi, only: MPI_INTEGER, MPI_DOUBLE_PRECISION, MPI_REAL4

    implicit none

    class(mpi_mod_test_type), intent(inout) :: this

    real(r_def) :: l_array(3) = (/ 2.0_r_def, 3.0_r_def, 1.0_r_def /)
    real(r_def) :: l_sum, l_min, l_max
    real(r_def) :: answer
    real(r_def) :: r_buffer(1)
    integer(i_def) :: l_array_i(3) = (/ 2_i_def, 3_i_def, 1_i_def /)
    integer(i_def) :: i_buffer(1),i_buffer_ans(1)
    integer(i_def) :: i, num_pes, mpi_datatype
    integer(i_def) :: l_sum_i, answer_i
    character(len=2) :: str_buffer(1)
    integer(i_def) :: rank_number, total_ranks

    l_sum=0.0_r_def
    do i=1,size(l_array)
      l_sum = l_sum + l_array(i)
    end do
    call global_sum( l_sum, answer )
    @assertEqual( 6.0_r_def, answer, 1.0e-2_r_def )

    l_sum_i=0_i_def
    do i=1,size(l_array_i)
      l_sum_i = l_sum_i + l_array_i(i)
    end do
    call global_sum( l_sum_i, answer_i )
    @assertEqual( 6_i_def, answer_i )

    l_max=l_array(1)
    do i=2,size(l_array)
      if(l_array(i) > l_max) l_max = l_array(i)
    end do
    call global_max( l_max, answer )
    @assertEqual( 3.0_r_def, answer, 1.0e-2_r_def )

    l_min=l_array(1)
    do i=2,size(l_array)
      if(l_array(i) < l_min) l_min = l_array(i)
    end do
    call global_min( l_min, answer )
    @assertEqual( 1.0_r_def, answer, 1.0e-2_r_def )

    i_buffer(1) = 7_i_def
    call all_gather(i_buffer, i_buffer_ans, 1)
    @assertEqual( 7_i_def, i_buffer_ans(1) )

    i_buffer(1) = 9_i_def
    call broadcast( i_buffer, 1, 0 )
    @assertEqual( 9_i_def, i_buffer(1) )

    r_buffer(1) = 11.0_r_def
    call broadcast( r_buffer, 1, 0 )
    @assertEqual( 11.0_r_def, r_buffer(1) )

    str_buffer(1) = 'Hi'
    call broadcast( str_buffer, len(str_buffer(1))*size(str_buffer), 0 )
    @assertEqual( 'Hi', str_buffer(1) )

    mpi_datatype=get_mpi_datatype( integer_type, i_def )
    @assertEqual( MPI_INTEGER, mpi_datatype )

    mpi_datatype=get_mpi_datatype( real_type, r_double )
    @assertEqual( MPI_DOUBLE_PRECISION, mpi_datatype )

    mpi_datatype=get_mpi_datatype( real_type, r_single )
    @assertEqual( MPI_REAL4, mpi_datatype )

    total_ranks=get_comm_size()
    @assertEqual( 1, total_ranks )

    rank_number=get_comm_rank()
    @assertEqual( 0, rank_number )

  end subroutine test_all

end module mpi_mod_test
