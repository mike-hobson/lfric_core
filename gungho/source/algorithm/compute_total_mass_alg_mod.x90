!-----------------------------------------------------------------------------
! (C) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Integrate a W3 field using either finite element or
!!        finite difference formulations.
module compute_total_mass_alg_mod

  use constants_mod, only: r_def, i_def

  implicit none

  private
  public :: compute_total_mass_alg

contains

  !> @details Compute the total mass of dry air, using either
  !!          quadrature on the finite element representation,
  !!          or a finite difference calculation to mimic
  !!          UM(ENDGame).
  !> @param[out] total_mass Total dry mass of the atmosphere
  !> @param[in]  rho        Density of dry component of atmosphere
  !> @param[in]  mesh       Mesh information
  subroutine compute_total_mass_alg( total_mass, rho, mesh )

    use compute_column_integral_kernel_mod, &
                                        only: compute_column_integral_kernel_type
    use compute_total_mass_kernel_mod,  only: compute_total_mass_kernel_type
    use energy_correction_config_mod,   only: integral_method,    &
                                              integral_method_fd, &
                                              integral_method_fe
    use extrusion_mod,                  only: TWOD
    use fs_continuity_mod,              only: W3, Wtheta
    use field_mod,                      only: field_type
    use finite_element_config_mod,      only: element_order, &
                                              nqp_exact
    use function_space_mod,             only: function_space_type
    use function_space_collection_mod,  only: function_space_collection
    use geometric_constants_mod,        only: get_coordinates, &
                                              get_height,      &
                                              get_panel_id
    use log_mod,                        only: log_event,         &
                                              log_scratch_space, &
                                              LOG_LEVEL_ERROR
    use mesh_mod,                       only: mesh_type
    use mesh_collection_mod,            only: mesh_collection
    use physical_op_constants_mod,      only: get_da_msl_proj
    use planet_config_mod,              only: radius
    use quadrature_xyoz_mod,            only: quadrature_xyoz_type
    use quadrature_rule_gaussian_mod,   only: quadrature_rule_gaussian_type

    implicit none

    type( field_type ),    intent(in)          :: rho
    type( mesh_type ),     intent(in), pointer :: mesh
    real( kind=r_def ),    intent(out)         :: total_mass

    ! For finite difference calculations
    type( field_type ), pointer :: height_w3 => null()
    type( field_type ), pointer :: height_wth => null()
    type( field_type ), pointer :: dA => null()
    type( field_type )          :: tot_col_mass
    type( mesh_type ),  pointer :: twod_mesh => null()

    ! For finite element calculations
    type( quadrature_xyoz_type )          :: qr
    type( quadrature_rule_gaussian_type ) :: quadrature_rule
    type( field_type ),           pointer :: chi(:) => null()
    type( field_type ),           pointer :: panel_id => null()
    type( function_space_type ),  pointer :: w3_fs => null()
    type( field_type )                    :: mass

    integer( kind=i_def ) :: mesh_id

    mesh_id = mesh%get_id()

    select case( integral_method )
    case( integral_method_fd )
      height_w3  => get_height( W3, mesh_id )
      height_wth => get_height( Wtheta, mesh_id )
      twod_mesh  => mesh_collection%get_mesh( mesh, TWOD )
      dA         => get_da_msl_proj( twod_mesh%get_id() )

      call tot_col_mass%initialise( vector_space = &
                                    function_space_collection%get_fs( twod_mesh, 0, W3 ) )

      total_mass = 0.0_r_def

      call invoke( compute_column_integral_kernel_type( rho, height_w3, height_wth, &
                                                        tot_col_mass, radius ),     &
                   inc_X_times_Y( tot_col_mass, dA ),                               &
                   sum_X( total_mass, tot_col_mass ) )

      nullify( height_w3, height_wth, twod_mesh, dA )
    case( integral_method_fe )
      ! Get a quadrature rule
      qr = quadrature_xyoz_type( nqp_exact, quadrature_rule )
      w3_fs    => function_space_collection%get_fs( mesh, element_order, W3 )
      chi      => get_coordinates( mesh_id )
      panel_id => get_panel_id( mesh_id )

      ! Initialise Mass
      call mass%initialise( vector_space = w3_fs )

      ! Compute conserved quantities
      call invoke( name = "Compute conserved quantities",          &
      ! Compute Total Mass
                   compute_total_mass_kernel_type( mass, rho, chi, &
                                                   panel_id, qr),  &
                   sum_X( total_mass, mass ) )

      nullify( chi, panel_id, w3_fs )
    case default
      write( log_scratch_space, '(''Unknown integral_method = '', i0, &
                                 ''in compute_total_mass.'')' ) integral_method
      call log_event( log_scratch_space, LOG_LEVEL_ERROR )
    end select

  end subroutine compute_total_mass_alg

end module compute_total_mass_alg_mod
