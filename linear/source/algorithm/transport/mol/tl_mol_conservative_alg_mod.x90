!-----------------------------------------------------------------------------
! (c) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Contains T.L. MOL routine for conservative transport of fields.

module tl_mol_conservative_alg_mod

  use constants_mod,                  only: r_def, i_def, l_def
  use log_mod,                        only: log_event,         &
                                            LOG_LEVEL_ERROR
  use field_mod,                      only: field_type
  use runge_kutta_init_mod,           only: get_rk_transport_weights
  use io_config_mod,                  only: subroutine_timers
  use operator_mod,                   only: operator_type
  use timer_mod,                      only: timer
  use fem_constants_mod,              only: get_directional_im3_div
  use dg_matrix_vector_kernel_mod,    only: dg_matrix_vector_kernel_type
  use transport_metadata_mod,         only: transport_metadata_type
  use advective_and_flux_alg_mod,     only: advective_and_flux_alg
  use tl_advective_and_flux_alg_mod,  only: tl_advective_and_flux_alg
  use mesh_mod,                       only: mesh_type
  use tl_transport_runtime_collection_mod, &
                                      only: tl_transport_runtime
  use transport_runtime_alg_mod,      only: transport_runtime_type
  use transport_runtime_collection_mod, &
                                      only: get_transport_runtime
  use transport_config_mod,           only: runge_kutta_method, &
                                            dry_field_name,     &
                                            advective_then_flux
  use psykal_lite_mod,                only: invoke_impose_min_flux_kernel_type
  use boundaries_config_mod,          only: limited_area
  use base_mesh_config_mod,           only: topology, topology_non_periodic

  implicit none

  public  :: tl_mol_conservative_alg

  contains

  !> @brief Tangent linear transport (conservative-form) with the MOL scheme.
  !> @details Performs a whole time step transport for the tangent linear model.
  !!          This solves the conservative transport equation with the specified
  !!          options in the transport_metadata.
  !> @param[in,out] field_np1   ACTIVE  Field at the end of the time step
  !> @param[in]     field       ACTIVE  Field at the start of the transport step
  !> @param[in]     ls_field    PASSIVE Field at the start of the transport step
  !> @param[in]     direction   Direction of advection (vertical, horizontal, or 3d)
  !> @param[in]     transport_metadata
  !!                            Contains the configuration options for
  !!                            transporting these fields
  subroutine tl_mol_conservative_alg( field_np1, field, ls_field, &
                                      direction, transport_metadata )

    implicit none

    type(field_type), intent(inout)              :: field_np1
    type(field_type), intent(in)                 :: field
    type(field_type), intent(in)                 :: ls_field
    integer(i_def),   intent(in)                 :: direction
    type(transport_metadata_type), intent(in)    :: transport_metadata

    integer(i_def)                :: mesh_id
    type(field_type),     pointer :: advecting_wind => null()
    type(field_type),     pointer :: ls_advecting_wind => null()
    type(field_type),     pointer :: old_flux_inc => null()
    type(field_type),     pointer :: field_start => null()
    type(operator_type),  pointer :: div => null()
    type(field_type)              :: flux
    type(field_type)              :: ls_flux_inc, flux_inc, adv_inc
    type(field_type)              :: sum_flux, ls_sum_flux
    type(field_type)              :: rhs, field_n
    type(field_type)              :: ls_field_np1, ls_field_n
    type(field_type), allocatable :: rk_field(:)
    real(r_def),      allocatable :: rk_weights(:,:)
    type(field_type), allocatable :: stored_ls_field(:,:)
    type(field_type)              :: flux_step
    integer(i_def)                :: stage, s, step
    integer(i_def)                :: nstage, substep
    real(r_def)                   :: inv_num_substeps
    integer(i_def)                :: number_substeps
    real(r_def)                   :: dt_substep
    logical(l_def)                :: enforce_min_value
    type(mesh_type),      pointer :: mesh => null()
    type(transport_runtime_type), pointer :: transport_runtime => null()
    type(field_type)              :: rhs_field
    logical(l_def)                :: final_rk_stage
    logical(l_def)                :: final_substep
    logical(l_def)                :: final_split_step
    logical(l_def)                :: do_flux
    logical(l_def)                :: do_advective

    if ( subroutine_timers ) call timer('tl_mol_conservative_alg')

    ! ------------------------------------------------------------------------ !
    ! Extract transport metadata and initialise temporary fields
    ! ------------------------------------------------------------------------ !
    mesh                          => field%get_mesh()
    transport_runtime             => get_transport_runtime(mesh)
    mesh_id                       = field%get_mesh_id()
    number_substeps               = transport_runtime%get_number_substeps(mesh_id,direction,transport_metadata%get_splitting())
    dt_substep                    = transport_runtime%get_dt_substep(mesh_id,direction,transport_metadata%get_splitting())
    enforce_min_value             = transport_metadata%get_enforce_min_value()
    advecting_wind                => tl_transport_runtime%get_directional_advecting_wind(mesh_id,direction)
    ls_advecting_wind             => transport_runtime%get_directional_advecting_wind(mesh_id,direction)
    inv_num_substeps              = 1.0_r_def/real(number_substeps, r_def)
    div                           => get_directional_im3_div(mesh_id, direction)

    call get_rk_transport_weights(nstage, rk_weights, runge_kutta_method)
    allocate( rk_field(nstage) )
    allocate( stored_ls_field(number_substeps,nstage) )

    do stage = 1, nstage
      call rk_field(stage)%initialise(field%get_function_space())
      do substep = 1, number_substeps
        call stored_ls_field(substep, stage)%initialise(field%get_function_space())
      end do
    end do
    call rhs_field%initialise( field%get_function_space() )

    call flux%initialise(advecting_wind%get_function_space())
    call flux_step%initialise(advecting_wind%get_function_space())
    call rhs%initialise(field%get_function_space())
    call field_n%initialise(field%get_function_space())
    call adv_inc%initialise(field%get_function_space())
    call flux_inc%initialise(field%get_function_space())
    call ls_flux_inc%initialise(field%get_function_space())
    call sum_flux%initialise(advecting_wind%get_function_space())
    call ls_sum_flux%initialise(advecting_wind%get_function_space())

    call field%copy_field(field_np1)
    call field%copy_field(field_n)
    call ls_field%copy_field(ls_field_np1)

    call invoke( setval_c(sum_flux, 0.0_r_def) )
    call invoke( setval_c(ls_sum_flux, 0.0_r_def) )
    call invoke( setval_c(flux_inc, 0.0_r_def) )
    call invoke( setval_c(ls_flux_inc, 0.0_r_def) )

    ! Can use either transport_runtime or tl_transport_runtime to determine this
    ! as both should give the same value
    final_split_step = ( transport_runtime%get_tracer_step_ctr() &
                         == transport_runtime%get_num_dry_steps() )

    ! ======================================================================== !
    !                    Linearisation state
    !
    !       This calculates the values of ls_field. These are stored as an
    !       array for each substep and each RK stage to use in the perturbation
    ! ======================================================================== !

    ! ------------------------------------------------------------------------ !
    ! Start of substepping loop
    ! ------------------------------------------------------------------------ !
    ls_substep_loop: do substep = 1, number_substeps

      final_substep = ( substep == number_substeps )

      ! Reset field_n ready for the this substep
      call ls_field_np1%copy_field(ls_field_n)

      ls_stage_loop: do stage = 1, nstage

        final_rk_stage = ( stage == nstage )

        ! Store values for use in the perturbation code. This is done before
        ! advecting so as to pick up the correct linearisation state
        call invoke( setval_X(stored_ls_field(substep,stage), ls_field_np1) )

        ! Compute the field for this stage:
        ! rhs_field = sum(s=1,stage): a(stage,s)*field^(s)
        call invoke( setval_X( rk_field(stage), ls_field_np1 ), &
                     setval_c( rhs_field, 0.0_r_def ) )
        do s = 1, stage
          call invoke( inc_X_plus_bY( rhs_field, rk_weights(stage,s), rk_field(s) ) )
        end do

        ! -------------------------------------------------------------------- !
        ! Apply advection / flux operator depending on formulation
        ! -------------------------------------------------------------------- !
        if (.not. advective_then_flux) then
          ! Old behaviour: entirely conservative form of equation
          do_flux = .true.
          do_advective = .false.

        else if (.not. final_rk_stage) then
          ! Advective-then-flux form but not last stage: just advective
          do_flux = .false.
          do_advective = .true.

        else if ( (.not. final_split_step) .or. (.not. final_substep) ) then
          ! Advective-then-flux form and last stage: both advective and flux
          do_flux = .true.
          do_advective = .true.

        else
          ! Last stage of last substep for either 3D MoL or last step of split scheme
          ! Just use flux form
          do_flux = .true.
          do_advective = .false.

        end if

        call advective_and_flux_alg(flux_step, adv_inc, rhs_field, ls_field_n, &
                                    ls_advecting_wind, direction,              &
                                    transport_metadata, final_rk_stage,        &
                                    dt_substep, do_flux, do_advective)

        if ( do_flux ) then
          call invoke( dg_matrix_vector_kernel_type(rhs, flux_step, div), &
                       X_minus_bY(ls_field_np1, ls_field_n, dt_substep, rhs) )
        end if
        if ( do_advective ) then
          call invoke( X_minus_bY(ls_field_np1, ls_field_n, dt_substep, adv_inc) )
        end if

        ! -------------------------------------------------------------------- !
        ! Blending zone update
        ! -------------------------------------------------------------------- !
        if ( limited_area .and. topology == topology_non_periodic ) then
          call log_event('T.L.: limited area in mol conservative not ' // &
                         'yet implemented', LOG_LEVEL_ERROR)
        end if

      end do ls_stage_loop

      ! Increment the total flux by the substep flux
      call invoke( inc_X_plus_bY(ls_sum_flux, inv_num_substeps, flux_step), &
      ! Increment the total flux increment
                   inc_X_minus_bY(ls_flux_inc, dt_substep, rhs) )

      ! ---------------------------------------------------------------------- !
      ! Blending zone update
      ! ---------------------------------------------------------------------- !
      if ( limited_area .and. topology == topology_non_periodic ) then
        call log_event('T.L.: limited area in mol conservative not ' // &
                        'yet implemented', LOG_LEVEL_ERROR)
      end if
    end do ls_substep_loop

    ! Enforce a minimum value if required while still conservative
    if ( enforce_min_value ) then
      call log_event('T.L.: enforcing min value in mol conservative not ' // &
                     'yet implemented', LOG_LEVEL_ERROR)
    end if

    ! ------------------------------------------------------------------------ !
    ! For the last split step, flux form and sum flux increments
    ! ------------------------------------------------------------------------ !
    if ( final_split_step .and. advective_then_flux ) then
      ! Get final field at end of transport
      ! field_np1 = field + sum(flux_inc)
      field_start => transport_runtime%get_field_n(mesh_id)
      call invoke( X_plus_Y(ls_field_np1, field_start, ls_flux_inc))
      do step = 1, transport_runtime%get_num_dry_steps() - 1
        old_flux_inc => transport_runtime%get_flux_inc(mesh_id, step)
        call invoke( inc_X_plus_Y(ls_field_np1, old_flux_inc) )
      end do
    end if

    ! ======================================================================== !
    !                    Perturbation
    !
    !   Repeat the calculation, but now for the tangent linear - adding in
    !   the values of ls_field_np1(substep,stage) obtained in the
    !   linearisation stage stage
    ! ======================================================================== !

    ! ------------------------------------------------------------------------ !
    ! Start of substepping loop
    ! ------------------------------------------------------------------------ !
    substep_loop: do substep = 1, number_substeps

      final_substep = ( substep == number_substeps )

      ! Reset field_n ready for the this substep
      call field_np1%copy_field(field_n)

      stage_loop: do stage = 1, nstage

        final_rk_stage = ( stage == nstage )

        ! Compute the field for this stage:
        ! rhs_field = sum(s=1,stage): a(stage,s)*field^(s)
        call invoke( setval_X( rk_field(stage), field_np1 ), &
                     setval_c( rhs_field, 0.0_r_def ) )
        do s = 1, stage
          call invoke( inc_X_plus_bY( rhs_field, rk_weights(stage,s), rk_field(s) ) )
        end do

        ! -------------------------------------------------------------------- !
        ! Apply advection / flux operator depending on formulation
        ! -------------------------------------------------------------------- !
        if (.not. advective_then_flux) then
          ! Old behaviour: entirely conservative form of equation
          do_flux = .true.
          do_advective = .false.

        else if (.not. final_rk_stage) then
          ! Advective-then-flux form but not last stage: just advective
          do_flux = .false.
          do_advective = .true.

        else if ( (.not. final_split_step) .or. (.not. final_substep) ) then
          ! Advective-then-flux form and last stage: both advective and flux
          do_flux = .true.
          do_advective = .true.

        else
          ! Last stage of last substep for either 3D MoL or last step of split scheme
          ! Just use flux form
          do_flux = .true.
          do_advective = .false.

        end if
        call tl_advective_and_flux_alg(flux_step, adv_inc, rhs_field,  &
                                       advecting_wind,                 &
                                       stored_ls_field(substep,stage), &
                                       ls_advecting_wind, direction,   &
                                       transport_metadata, final_rk_stage, &
                                       dt_substep, do_flux, do_advective)

        if ( do_flux ) then
          call invoke( dg_matrix_vector_kernel_type(rhs, flux_step, div), &
                       X_minus_bY(field_np1, field_n, dt_substep, rhs) )
        end if
        if ( do_advective ) then
          call invoke( X_minus_bY(field_np1, field_n, dt_substep, adv_inc) )
        end if

        ! -------------------------------------------------------------------- !
        ! Blending zone update
        ! -------------------------------------------------------------------- !
        if ( limited_area .and. topology == topology_non_periodic ) then
          call log_event('T.L.: limited area in mol conservative not ' // &
                         'yet implemented', LOG_LEVEL_ERROR)
        end if

      end do stage_loop

      ! Increment the total flux by the substep flux
      call invoke( inc_X_plus_bY(sum_flux, inv_num_substeps, flux_step), &
      ! Increment the total flux increment
                   inc_X_minus_bY(flux_inc, dt_substep, rhs) )

      ! ---------------------------------------------------------------------- !
      ! Blending zone update
      ! ---------------------------------------------------------------------- !
      if ( limited_area .and. topology == topology_non_periodic ) then
        call log_event('T.L.: limited area in mol conservative not ' // &
                        'yet implemented', LOG_LEVEL_ERROR)
      end if
    end do substep_loop

    ! Enforce a minimum value if required while still conservative
    if ( enforce_min_value ) then
      call log_event('T.L.: enforcing min value in mol conservative not ' // &
                     'yet implemented', LOG_LEVEL_ERROR)
    end if

    ! ------------------------------------------------------------------------ !
    ! For the last split step, flux form and sum flux increments
    ! ------------------------------------------------------------------------ !
    if ( final_split_step .and. advective_then_flux ) then
      ! Get final field at end of transport
      ! field_np1 = field + sum(flux_inc)
      field_start => tl_transport_runtime%get_field_n(mesh_id)
      call invoke( X_plus_Y(field_np1, field_start, flux_inc))
      do step = 1, tl_transport_runtime%get_num_dry_steps() - 1
        old_flux_inc => tl_transport_runtime%get_flux_inc(mesh_id, step)
        call invoke( inc_X_plus_Y(field_np1, old_flux_inc) )
      end do
    end if

    ! ------------------------------------------------------------------------ !
    ! End of split step things
    ! ------------------------------------------------------------------------ !

    ! Store field in tl_transport_runtime for conservative transport
    ! if this is the underlying dry field
    if (trim(transport_metadata%get_name()) == trim(dry_field_name)) then
      call transport_runtime%set_dry_fields(ls_field, ls_field_np1, ls_sum_flux)
      call tl_transport_runtime%set_dry_fields(field, field_np1, sum_flux)
    end if

    call tl_transport_runtime%set_flux_inc(flux_inc)
    call transport_runtime%set_flux_inc(ls_flux_inc)
    call tl_transport_runtime%inc_tracer_step_ctr()
    call transport_runtime%inc_tracer_step_ctr()

    if ( allocated(rk_field) ) deallocate(rk_field)
    if ( allocated(rk_weights) ) deallocate(rk_weights)
    if ( allocated(stored_ls_field) ) deallocate(stored_ls_field)
    nullify( div, advecting_wind, ls_advecting_wind )

    if ( subroutine_timers ) call timer('tl_mol_conservative_alg')

  end subroutine tl_mol_conservative_alg

end module tl_mol_conservative_alg_mod
